grammar Ruletxt

Document  <- __ (Rule __)* %document
Rule      <- IfBlock / Assignment / Comment / EmptyConclusion

Assignment <- '[OPM-conclusion]' __ attribute __ '=' __ (OperatorExpression / BracketedOperatorExpression / Comparison / Expression) %assignment
IfBlock <- '[OPM-conclusion]' __ attribute __ 'if' __ (OperatorExpression / BracketedOperatorExpression / Comparison / Expression) %if_block

OperatorExpression <- OPMLevel __ AllEtc? __ (BracketedOperatorExpression / Comparison / Expression) OPMLevel? __ (Operator __ OPMLevel? (BracketedOperatorExpression / Comparison / Expression))+ %operator_expression

Comparison <- OPMLevel? __ Expression __ Comparator __ Expression %comparison

Expression <-  __ (date / attribute / string) __ %expression

### Bracketed sections
# These are complicated due to the line headers. Example:
# [OPM-level3(]                   the client and/or partner do not have a letter confirming current receipt of a passporting benefit
# [OPM-level3]                    and
# [OPM-level3]                    the passported benefit being received is not Income Support
# [OPM-level2)]
BracketedOperatorExpression     <- AllEtc? __ OPMLevelUpToBrackets? "(" "]"? (Comparison / Expression) OPMLevel? __ (Operator __ OPMLevel? (Comparison / Expression))+ __ OPMLevelUpToBrackets? ")" "]"? %bracketed_operator_expression
OPMLevel <- "[OPM-level" [\d] "]"
OPMLevelUpToBrackets <- "[OPM-level" [\d]

Comment <- ( "[OPM-blankline]" / "[OPM-commentary]" / "[OPM-RuleName]" / ("[OPM-Heading" [\d]? "]") / ("[TOC" [\d]? "]") ) [ ]* CommentText? %comment
CommentText <- [^\n]+
EmptyConclusion <- '[OPM-conclusion]' [ ]* [\n] %empty_conclusion

AllEtc  <- "all" / "both" / "any" / "either"
Operator <- "and" / "or"
Comparator <- "<>" / "<=" / ">=" / "<" / ">"
string    <-  quote ("\\" . / notaquote)* quote %string
quote     <- ["“”]
notaquote <- [^"“”]
date      <- [\d]{4} "-" [\d]{2} "-" [\d]{2} %date
attribute <- attribute_word (!(" if" / " and" / " or") " " attribute_word)* %attribute
attribute_word <- [^=<>"“”\[\]\n ]+
__        <-  [\s]*

